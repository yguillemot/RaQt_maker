
# Generate the documentation file

use gene::config;
use gene::common;
use gene::natives;
use gene::replace;
use gene::ToRakuConversions;

# $api : The Qt API description (the output of the parser with the black and
# white info marks added)
# %exceptions : Exceptions if any
sub doc_generator(API :$api, :%exceptions) is export
{
    my %c = $api.qclasses;

    say "Generate the documentation : start";
    # say "\n" x 2;


    # Generate the title of the document

    my $title = RAQTNAME;
    my Str $out =   "$title\n" ~ ('=' x $title.chars) ~ "\n\n";

    $out ~= "Qt classes implemented by "
             ~ RAQTNAME ~ " V" ~ RAQTVERSION ~ "\n";
    $out ~= "generated by RaQt_maker V" ~ $geneVersion ~ "\n\n";

    my $tocTitle = "Table of content:";
    $out ~= "$tocTitle\n" ~ ('-' x $tocTitle.chars) ~ "\n\n";

    spurt TITLEFILE, $out;


    # Generate the core of the document

    $out = "\n";

    for %c.keys.sort -> $k {
        my $v = %c{$k};

        next if !$v.whiteListed || $v.blackListed;
        next if $v.name (elem) $specialClasses;

        # $k (eq $v.name) is the Qt class name
        my $title = "Class $k";
        $out ~= "\n$title\n" ~ ('-' x $title.chars) ~ "\n";

        # Print out the parents of the current class
        for $v.parents -> $p {
            $out ~= "\tinherits $p\n";
        }
        $out ~= "\n";

        # Print out the children of the current class
        my $first = True;
        for $v.children -> $c {
            next if !%c{$c}.whiteListed || %c{$c}.blackListed;
            next if $c (elem) $specialClasses;
            if $first {
                $first = False;
                $out ~= "\tinherited by $c";
            } else {
                $out ~= ", $c";
            }
        }
        $out ~= "\n\n";

        # Print out enums if any
        $out ~= showEnums $v;


        $out ~= "\n";


        class KnownMethods {
            has Function $.meth;    # Method
            has Str      $.from;    # Class where method is defined
        }

        # Gather methods
        my @mlist;
        MLOOP0: for $v.methods -> $m {
            next MLOOP0 if !$m.whiteListed || $m.blackListed;
            @mlist.push(KnownMethods.new(meth => $m, from => Str));
        }
        for $v.ancestors -> $a {
            MLOOP1: for %c{$a}.methods -> $m {
                next MLOOP1 if !$m.whiteListed || $m.blackListed;
                @mlist.push(KnownMethods.new(meth => $m, from => $a));
            }
        }


        # Loop on methods
        MLOOP: for @mlist -> $mth {

            my $m = $mth.meth;
            my $methodName = $m.name;

            if $m.name ~~ "ctor" {
                if $mth.from {
                    next MLOOP;     # Ctors are not inherited
                } else {
                    $methodName = $k;
                }
            }

            my $qualifiers = "";
            if $m.isSlot {
                $qualifiers ~= "[slot] ";
            }

            if $m.isSignal {
                if $m.isPrivateSignal {
                    $qualifiers ~= "[private signal] "
                } else {
                    $qualifiers ~= "[signal] "
                }
            }

            if $m.isVirtual {
                $qualifiers ~= "[virtual] ";
            }

            if $m.isStatic {
                $qualifiers ~= "[static] ";
            }

            if $m.isProtected {
                $qualifiers ~= "[protected] ";
            }

            if $mth.from {
                $qualifiers ~= "inherited from " ~ $mth.from;
            }

            my $qmeth = qRet($m)  ~ " $methodName"
                        ~ qSignature($m, showDefault => True,
                                    showParenth => True, startWithSep => False);

            if $m.name ~~ "ctor" {
                 $out ~= "#### Method " ~ $k ~ ".new"
                                            ~ strRakuArgsCtorDecl($m, $k, %c);
            } else {
                $out ~= "#### Method " ~ $methodName ~ strRakuArgsDecl($m, %c);
            }

            $out ~= "\n\t" ~ $qualifiers if $qualifiers ne "";
            $out ~= "\n\tcalls Qt method " ~ $qmeth ~ "\n\n";
        }

    }

    spurt DOCFILE, $out;

    say "Generate the documentation : end";
    say "";

}










